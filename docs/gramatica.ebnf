?start: statement*

?statement: asignacion
          | funcion
          | funcion_asincronica
          | clase
          | bucle_mientras
          | bucle_para
          | condicional
          | importacion
          | usar
          | macro
          | impresion
          | retorno
          | hilo
          | llamada
          | switch
          | try_catch
          | with_stmt
          | option
          | expr

asignacion: ("var"|"variable")? IDENTIFICADOR "=" expr
decorador: "@" IDENTIFICADOR
funcion: decorador* "func" IDENTIFICADOR "(" parametros? ")" ":" cuerpo "fin"
funcion_asincronica: decorador* "asincronico" "func" IDENTIFICADOR "(" parametros? ")" ":" cuerpo "fin"
clase: "clase" IDENTIFICADOR ":" cuerpo "fin"
bucle_mientras: "mientras" expr ":" cuerpo "fin"
bucle_para: "para" IDENTIFICADOR "in" expr ":" cuerpo "fin"
condicional: "si" expr ":" cuerpo ("sino" ":" cuerpo)? "fin"
importacion: "import" CADENA
usar: "usar" CADENA
macro: "macro" IDENTIFICADOR "{" statement* "}"
impresion: "imprimir" "(" argumentos? ")"
retorno: "retorno" expr
hilo: "hilo" llamada
switch: "switch" expr ":" case+ ("sino" ":" cuerpo)? "fin"
case: "case" patron ("si" expr)? ":" cuerpo
patron: "_" | IDENTIFICADOR | valor | "(" patron ("," patron)* ")"
try_catch: ("try"|"intentar") ":" cuerpo ("catch"|"capturar") IDENTIFICADOR ":" cuerpo "fin"
with_stmt: ("with"|"con") expr (("as"|"como") IDENTIFICADOR)? ":" cuerpo "fin"
option: "option" IDENTIFICADOR "=" expr
llamada: IDENTIFICADOR "(" argumentos? ")"
cuerpo: statement*
parametros: IDENTIFICADOR ("," IDENTIFICADOR)*
argumentos: expr ("," expr)*
?expr: valor (operador valor)*
?valor: CADENA
      | ENTERO
      | FLOTANTE
      | IDENTIFICADOR
      | llamada
      | holobit
      | lambda
      | "esperar" valor
      | "Some" "(" expr ")"
      | "None"
holobit: "holobit" "(" "[" [expr ("," expr)*] "]" ")"
lambda: "lambda" parametros? ":" expr
operador: "+"|"-"|"*"|"/"|">="|"<="|">"|"<"|"=="|"!="|"&&"|"||"

CADENA: /"[^"\n]*"|'[^'\n]*'/
ENTERO: /[0-9]+/
FLOTANTE: /[0-9]+\.[0-9]+/
IDENTIFICADOR: /[^\W\d][\w]*/

%ignore /\s+/
